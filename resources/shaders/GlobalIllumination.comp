layout(local_size_x = 8, local_size_y = 8) in;

// SET 0
#include "./GlobalDataBuffer.glsl"
#include "./CreateRay.glsl"
// SET 1
#include "./RayTracer.glsl"
#include "./Atmosphere.glsl"

#define LIGHTS_SET 2
layout(set = 3, binding = 0) uniform sampler2D gBufferMaterialA;// Albedo
layout(set = 4, binding = 0) uniform sampler2D gBufferMaterialB;// Normal; Roughness
layout(set = 5, binding = 0) uniform sampler2D gBufferMaterialD;// Position
layout(set = 6, binding = 0, rgba32f) uniform image2D surfaceCacheImage;
layout(set = 7, binding = 0, rgba32f) uniform image2D globalIlluminationImage;

#include "./LightsBuffer.glsl"

struct BounceInfo {
    vec3 albedo;
    bool isEmissive;
    vec3 hitNormal;
    vec3 currentPosition;
    vec3 throughput;
    vec3 indirectLight;
};

uint rngState;
void computeLightContribution(in Light l, in BounceInfo bounceInfo, inout vec3 throughput, inout vec3 indirectLight){
    float bias = max(.05, 1e-4 * length(bounceInfo.currentPosition.xyz));
    vec3 localHitPosition = bounceInfo.currentPosition.xyz + bounceInfo.hitNormal * bias;
    vec3 lightDir = normalize(l.position - localHitPosition);
    Ray ray = Ray(localHitPosition, lightDir, 1./lightDir);
    Hit hitData = traceAllTiles(ray);
    float lightDistance = length(l.position - localHitPosition);
    float hitDistance = length(hitData.hitPosition - localHitPosition);
    float localShadows = 1;
    if (hitData.anyHit && hitDistance < lightDistance) {
        localShadows *= clamp(1 / lightDistance, 0, 1);
    }
    if (localShadows > 0){
        float NdotL = max(dot(bounceInfo.hitNormal, lightDir), 0.0);
        vec3 lightColorContribution = l.color * bounceInfo.albedo / PI;
        vec3 lightContribution = lightColorContribution * NdotL / length(l.position - localHitPosition);
        indirectLight += throughput * lightContribution;
        throughput *= lightColorContribution;
    }
}

float getMaxSamples(in vec3 currentPosition){
    float dist = length(globalData.cameraWorldPosition - currentPosition);
    if (dist < 2){
        return 160;
    }
    if (dist < 4){
        return 320;
    }
    return 540;
}

void evaluateSurface(in Light sunLight, inout BounceInfo bounceInfo){
    ivec2 coord = ivec2(worldToSurfaceCacheHash(bounceInfo.currentPosition) * vec2(globalData.surfaceCacheWidth, globalData.surfaceCacheHeight));

    vec4 previousCacheData = imageLoad(surfaceCacheImage, coord);
    float accumulationCount = previousCacheData.a;

    if (length(previousCacheData.rgb) == 0 || bounceInfo.isEmissive){
        vec3 lIndirect = vec3(0);
        vec3 lT = vec3(1);
        if (bounceInfo.isEmissive){
            vec3 localIndirect = bounceInfo.albedo * globalData.giEmissiveFactor;
            lIndirect += bounceInfo.throughput * localIndirect;
            lT *= bounceInfo.albedo * globalData.giEmissiveFactor;
        } else {
            if (globalData.lightsQuantity > 0){
                for (int i = 0; i < globalData.lightsQuantity; ++i) {
                    Light l = lightsBuffer.lights[i];
                    computeLightContribution(l, bounceInfo, lT, lIndirect);
                }
            }
            if (globalData.enabledSun){
                computeLightContribution(sunLight, bounceInfo, lT, lIndirect);
            }
        }

        vec3 accumulatedResult = previousCacheData.rgb * (1. - 1./accumulationCount) + lIndirect * 1./accumulationCount;
        imageStore(surfaceCacheImage, coord, vec4(accumulatedResult, accumulationCount + 1));
        bounceInfo.indirectLight += accumulatedResult;
        bounceInfo.throughput *= lT;
    }
    bounceInfo.indirectLight += previousCacheData.rgb;
    bounceInfo.throughput *= previousCacheData.rgb;
}

vec3 pathTraceScene(vec3 surfaceAlbedo, vec3 firstHitNormal, vec3 firstHitPos, float roughness, vec3 rayDir) {
    Light sunLight = Light(globalData.sunColor, globalData.sunPosition, -1, -1, -1);


    BounceInfo bounceInfo;
    bounceInfo.throughput = vec3(1);
    bounceInfo.currentPosition = firstHitPos;
    bounceInfo.hitNormal = firstHitNormal;

    float localRoughness = roughness;
    vec3 localRayDir = rayDir;
    vec3 randomDir;

    for (uint j = 0; j < globalData.giBounces; j++){
        vec3 diffuseRayDir = normalize(bounceInfo.hitNormal + RandomUnitVector(rngState));
        vec3 specularRayDir = reflect(localRayDir, bounceInfo.hitNormal);

        specularRayDir = normalize(mix(specularRayDir, diffuseRayDir, localRoughness * localRoughness));
        localRayDir = mix(diffuseRayDir, specularRayDir, 1. - localRoughness);
        Ray ray = Ray(bounceInfo.currentPosition, localRayDir, 1.0 / localRayDir);
        Hit hitData = traceAllTiles(ray);
        if (!hitData.anyHit) {
            if (globalData.enabledSun){
                bounceInfo.albedo = calculate_sky_luminance_rgb(normalize(globalData.sunPosition), ray.d, 2.0f) * 0.05f;
                bounceInfo.isEmissive = true;
                evaluateSurface(sunLight, bounceInfo);
            }
            break;
        }

        localRoughness = .5;

        VoxelMaterialData matData = unpackVoxel(hitData);

        bounceInfo.albedo = matData.albedo;
        bounceInfo.isEmissive = matData.isEmissive;
        bounceInfo.hitNormal = matData.normal;
        bounceInfo.currentPosition = hitData.hitPosition;

        evaluateSurface(sunLight, bounceInfo);
    }
    return bounceInfo.indirectLight / globalData.giBounces;
}

void main() {
    vec2 texCoords = vec2(gl_GlobalInvocationID.xy) / imageSize(globalIlluminationImage);
    vec4 materialD = texture(gBufferMaterialD, texCoords);
    if (materialD.a != 1.){
        return;
    }
    vec4 albedoEmissive = texture(gBufferMaterialA, texCoords);
    if (albedoEmissive.a != 0){
        return;
    }

    vec3 hitPosition = materialD.rgb;
    vec4 finalColor = vec4(0, 0, 0, 1);
    rngState = uint(uint(gl_GlobalInvocationID.x) * uint(1973) + uint(gl_GlobalInvocationID.y) * uint(9277) + uint(globalData.globalFrameCount) * uint(26699)) | uint(1);

    float bias = max(.05, 1e-4 * length(hitPosition.xyz));
    vec4 normalRoughness = texture(gBufferMaterialB, texCoords);
    vec3 normal = normalRoughness.rgb;
    vec3 currentPosition = hitPosition.xyz + normal * bias;

    vec4 previousFrame = imageLoad(globalIlluminationImage, ivec2(gl_GlobalInvocationID.xy));
    float accumulationCount = previousFrame.a;

    vec3 rayDir = createRay(texCoords, globalData.invProj, globalData.invView);
    vec3 currentFrame = pathTraceScene(albedoEmissive.rgb, normal, currentPosition, normalRoughness.a, rayDir);
    vec3 accumulatedResult = previousFrame.rgb * (1. - 1./accumulationCount) + currentFrame * 1./accumulationCount;


    imageStore(globalIlluminationImage, ivec2(gl_GlobalInvocationID.xy), vec4(accumulatedResult, accumulationCount + 1));
}